📦 allegro 🔴

🐇 🦁 🍇
  🍰 x 🚀
  🍰 y 🚀
  🍰 monster 🖼

  🐈 🆕 🍇
    🍮 x 500
    🍮 y 500
    🍮 monster 🚇🔷🖼📄 🔤cookie_monster.png🔤
  🍉

  🐖 🖌 app 🏔 🍇
    📼 app monster x y
  🍉

  🐖 ⬇️ 🍇
    🍮 y ➕ y 10
  🍉

  🐖 ⬆️ 🍇
    🍮 y ➖ y 10
  🍉

  🐖 ⬅️ 🍇
    🍮 x ➖ x 10
  🍉

  🐖 ➡️ 🍇
    🍮 x ➕ x 10
  🍉

  🐖 👉️ ➡️ 🚀 🍇
    🍎 ➕ x 60
  🍉

  🐖 👇 ➡️ 🚀 🍇
    🍎 ➕ y 70
  🍉
🍉


🐇 🍪 🍇
  🍰 cookie 🖼
  🍰 x 🚀
  🍰 y 🚀

  🐈 🆕 🍼 x 🚀 🍼 y 🚀 🍼 cookie 🖼  🍇🍉

  🐖 🖊 app 🏔 monsterX 🚀 monsterY 🚀 ➡️ 👌 🍇
    🍊 🎊🎊➡️ monsterX x ⬅️ monsterX ➕ x 50 🎊➡️ monsterY y ⬅️ monsterY ➕ y 50 🍇
      🍎 👍
    🍉
    📼 app cookie x y
    🍎 👎
  🍉
🍉

🏁 🍇
  🍩🙋🏔 🍇 app 🏔
    🍦 display 🔷📺🆕 1000 1000
    🏷 display 🔤Cookie Monster Game🔤

    🍦 cookieBmp 🚇🔷🖼📄 🔤cookie.png🔤

    🍦 cookies 🔷🍨🐚🍪🐸
    🍦 prng 🔷🎰 🆕
    🔂 i ⏩ 0 10 🍇
      🐻 cookies 🔷🍪🆕 🚀🚂 prng 50 900 🚀🚂 prng 50 900 cookieBmp
    🍉

    🍦 sample 🚇🔷🎶📄 🔤sound.wav🔤

    🍦 font 🚇🔷🕉📄 🔤Monoton-Regular.ttf🔤 63
    🍦 monster 🔷🦁🆕

    🍦 queue 🔷🗃🆕
    ⌨️ queue
    🔁 👍 🍇
      🚿 app 🔷🎨🆕 255 255 255 255
      🖌 monster app

      🍦 iterator 🔷🌱🐚🍪🆕 cookies
      🔂 cookie iterator 🍇
        🍊 🖊 🍺cookie app 👉️ monster 👇 monster 🍇
          🚯 iterator
          🍊 ▶️ 🐔 cookies 0 🍇
            🏁 sample 1 0 1
          🍉
        🍉
      🍉
      🔡 app font 🔡 🐔 cookies 10 990 10 🔷⚖➡️ 🔷🎨🆕 0 0 0 255
      🎦 app

      🍊 😛 🐔 cookies 0 🍇
        🚿 app 🔷🎨🆕 4 115 187 255
        🔡 app font 🔤Well done!🔤 500 500 🔷⚖↔ 🔷🎨🆕 255 255 255 255
        🎦 app

        🍦 endSample 🚇🔷🎶📄 🔤sound_end.wav🔤
        🏁 endSample 1 0 1

        🔁 👍 🍇
          🍦 event ⏳ queue
          🍊🍦 keyboardEvent 🔲 event 📩 🍇
            🍊 🍦 key 🔣 keyboardEvent 🍇
              🍊 😛 key 🔟q 🍇
                🍎
              🍉
            🍉
          🍉
        🍉
        🍎
      🍉

      🍦 event ⏳ queue

      🍊🍦 keyboardEvent 🔲 event 📩 🍇
        🍊 😛📟 keyboardEvent 84 🍇
          ⬆️ monster
        🍉
        🍋 😛📟 keyboardEvent 85 🍇
          ⬇️ monster
        🍉
        🍋 😛📟 keyboardEvent 82 🍇
          ⬅️ monster
        🍉
        🍋 😛📟 keyboardEvent 83 🍇
          ➡️ monster
        🍉
      🍉
    🍉
  🍉
🍉
